#include "MacObjCCompat.hpp"
#include "app/nodes/controls/TextEditor.hpp"
#include "MacNodeHandlerEnsureTests.hpp"
#include "support/TestVerify.hpp"
#include "support/RailTextLayoutFixture.hpp"
#include <cmath>

#include <AppKit/AppKit.h>
#include <cstdio>

#include "MacScenePlatformController.hpp"
#include "app/nodes/Text.hpp"
#include "app/nodes/nestable/RowColumn.hpp"
#include "app/layout/FallbackControlMetrics.hpp"
#include "app/nodes/controls/Button.hpp"
#include "app/nodes/controls/ScrollBar.hpp"
#include "app/scene/projection/RetainedNodeHandler.hpp"

namespace
{
  void verifyFixedColumnFitsAtBothScales()
  {
    using namespace loka::app;
    const int count = 3;
    const int height = 40 + count * layout::FallbackControlMetrics::kButtonHeight
                       + (count - 1) * layout::FallbackControlMetrics::kVerticalSpacing;
    const RailMetrics metrics[] = {RailMetrics(), loka::macos::DefaultRailMetrics()};
    for (int scaleIndex = 0; scaleIndex < 2; ++scaleIndex)
    {
      const loka::macos::MacProjection allocation(0, metrics[scaleIndex]);
      NSView *root = [[NSView alloc] initWithFrame:allocation.projectClientSize(257, height).r];
      LOKA_VERIFY(root != nil);
      {
        MacScenePlatformController controller((void *)root, metrics[scaleIndex]);
        LOKA_VERIFY(controller.projection().clientCapacityToLu([root bounds].size.width) == 257);
        LOKA_VERIFY(controller.projection().clientCapacityToLu([root bounds].size.height) == height);
        if (scaleIndex == 1)
        {
          LOKA_VERIFY(controller.projection().projectEdge(8).pt == 10);
          LOKA_VERIFY(controller.projection().capacityToLu(301) == 240);
        }
        StackNode column((StackProps(STACK_AXIS_COLUMN)));
        for (int i = 0; i < count; ++i)
          column.addChild(new ButtonNode(ButtonProps()));
        controller.onChange(&column, loka::app::scene::NODE_DIRTY_NONE, false);
        controller.relayout(0, 0);
        NSArray *children = [root subviews];
        LOKA_VERIFY([children count] == static_cast<NSUInteger>(count));
        CGFloat bottom = 0;
        for (NSUInteger i = 0; i < [children count]; ++i)
        {
          const NSRect frame = [[children objectAtIndex:i] frame];
          if (NSMaxY(frame) > bottom)
            bottom = NSMaxY(frame);
        }
        LOKA_VERIFY(bottom == controller.projection().projectEdge(height - 20).pt);
        LOKA_VERIFY(bottom <= [root bounds].size.height);
        controller.onChange(0, loka::app::scene::NODE_DIRTY_NONE, false);
      }
      [root release];
    }
  }

  void verifyTextColumnExtents()
  {
    using namespace loka::app;
    const RailMetrics defaults = loka::macos::DefaultRailMetrics();
    // The middle leg isolates fontScale from spaceScale.
    const RailMetrics metrics[] = {RailMetrics(), RailMetrics(defaults.fontScale, Ratio()), defaults};
    const char *strings[] = {"First", "First\nSecond"};
    for (int scaleIndex = 0; scaleIndex < 3; ++scaleIndex)
      for (int boxed = 0; boxed < 2; ++boxed)
        for (int sample = 0; sample < 2; ++sample)
        {
          const loka::macos::MacProjection allocation(0, metrics[scaleIndex]);
          NSView *root = [[NSView alloc] initWithFrame:allocation.projectClientSize(340, 250).r];
          LOKA_VERIFY(root != nil);
          {
            MacScenePlatformController controller(root, metrics[scaleIndex]);
            RailTextLayoutFixture fixture(boxed != 0, strings[sample]);
            controller.onChange(&fixture.column, loka::app::scene::NODE_DIRTY_NONE, false);
            controller.relayout(0, 0);
            const loka::macos::MacProjection &projection = controller.projection();
            LOKA_VERIFY(projection.clientCapacityToLu([root bounds].size.height) == 250);
            NSArray *views = [root subviews];
            LOKA_VERIFY([views count] == static_cast<NSUInteger>(boxed ? 5 : 3));
            NSView *page = [views objectAtIndex:0];
            NSView *caption = [views objectAtIndex:1];
            NSButton *button = (NSButton *)[views objectAtIndex:boxed ? 3 : 2];
            LOKA_VERIFY([button isKindOfClass:[NSButton class]]);

            // Reference native measurement: pin both the inverse and the lu
            // padding, not an equality that independent font/space ratios lack.
            NSFont *font = (NSFont *)controller.textFont(FontSize<18>());
            NSTextFieldCell *cell = [[NSTextFieldCell alloc] initTextCell:@"First"];
            [cell setFont:font];
            [cell setWraps:YES];
            [cell setScrollable:NO];
            [cell setLineBreakMode:NSLineBreakByWordWrapping];
            const NSRect bounds = NSMakeRect(0, 0, projection.projectLength(0, 300).pt, 10000);
            const CGFloat oneLine = [cell cellSizeForBounds:bounds].height;
            [cell setStringValue:[NSString stringWithUTF8String:strings[sample]]];
            const CGFloat nativeHeight = [cell cellSizeForBounds:bounds].height;
            [cell release];
            // Short words cannot soft-wrap at 300 lu; the newline is the only
            // line break. NSCell's fixed padding can keep the ratio below two.
            const int lines = static_cast<int>(std::floor(nativeHeight / oneLine + 0.5));
            LOKA_VERIFY(lines == sample + 1);
            const int fontHeight = projection.measurementToLu(
                [font ascender] + std::fabs([font descender]) + [font leading]);
            const int minimum = fontHeight > 20 ? fontHeight : 20;
            const int measured = projection.measurementToLu(nativeHeight) + 2;
            const int textHeight = measured > minimum ? measured : minimum;
            const int captionY = boxed ? 190 : 20 + textHeight + 12;
            const int buttonY = captionY + 20 + 12;
            LOKA_VERIFY(NSEqualRects([page frame], projection.projectFrame(
                loka::core::Frame(20, 20, 300, textHeight)).r));
            LOKA_VERIFY([caption frame].origin.y == projection.projectEdge(captionY).pt);
            LOKA_VERIFY(NSMinY([button frame]) == projection.projectEdge(buttonY).pt);
            LOKA_VERIFY(NSMaxY([button frame]) == projection.projectEdge(buttonY + 32).pt);
            // The actual Scrapbook fixed Box prevents measured text changes
            // reaching the caption/buttons, for either one or two native lines.
            if (boxed)
              LOKA_VERIFY(buttonY + 32 == 254);
            std::printf("  Mac text extent: font=%d/%d space=%d/%d boxed=%d lines=%d "
                        "nativeText=%.2f textLu=%d buttonLu=%d..%d framePt=%.2f..%.2f clientPt=%.2f\n",
                        metrics[scaleIndex].fontScale.num, metrics[scaleIndex].fontScale.den,
                        metrics[scaleIndex].spaceScale.num, metrics[scaleIndex].spaceScale.den,
                        boxed, lines, static_cast<double>(nativeHeight), textHeight, buttonY, buttonY + 32,
                        static_cast<double>(NSMinY([button frame])), static_cast<double>(NSMaxY([button frame])),
                        static_cast<double>([root bounds].size.height));
            controller.onChange(0, loka::app::scene::NODE_DIRTY_NONE, false);
          }
          [root release];
        }
  }

  int gReentrantCreates = 0;
  int gReentrantAttachReads = 0;
  int gReentrantAfterAttaches = 0;
  int gReentrantDestroys = 0;

  class MacReentrantEnsureContext : public loka::app::scene::NodeContext
  {
  public:
    MacReentrantEnsureContext()
        : loka::app::scene::NodeContext()
    {
      ++gReentrantCreates;
    }

    virtual ~MacReentrantEnsureContext()
    {
      ++gReentrantDestroys;
    }

    void readLifecycleFactOnAttach()
    {
      ++gReentrantAttachReads;
    }
  };

  class MacReentrantEnsureHandler
      : public loka::app::scene::RetainedNodeHandler<MacReentrantEnsureHandler,
                                                     loka::app::ButtonNode,
                                                     MacReentrantEnsureContext>
  {
  public:
    static loka::app::ButtonNode *cast(loka::app::scene::Node *node)
    {
      return node ? node->asButtonNode() : 0;
    }

    static MacReentrantEnsureContext *create(loka::app::ButtonNode *,
                                             loka::app::scene::IPlatformController *,
                                             const loka::app::scene::LayoutState &)
    {
      return new MacReentrantEnsureContext();
    }

    static void afterAttach(MacReentrantEnsureContext *ctx)
    {
      ++gReentrantAfterAttaches;
      loka::app::scene::Node *owner = ctx->owner();
      LOKA_VERIFY(owner != 0);
      // Model the modal macOS dialog hazard from the ensure side: synchronous
      // app code REPLACES the node's live context (here with none) before
      // ensureContext returns, so the platform controller's local pointer is
      // stale on return. setContext(0) on a still-ATTACHED node is a live
      // replacement, not the retire door -- releaseContext's terminal
      // delivery assumes RETIRED was already written, so nothing is delivered
      // here. That is deliberate: this test owns the ensure re-read contract.
      // Retirement terminal delivery and queued native destruction are a
      // different contract, covered by the lifecycle-fact tests, and a
      // regression confined to those would not turn this test red.
      // Do not touch ctx after this call; it has been reclaimed.
      owner->setContext(0);
    }
  };

  NSUInteger countChildViews(NSView *root)
  {
    return [[root subviews] count];
  }

  bool frameEquals(NSView *view, CGFloat x, CGFloat y, CGFloat width, CGFloat height)
  {
    const NSRect frame = [view frame];
    return frame.origin.x == x && frame.origin.y == y && frame.size.width == width && frame.size.height == height;
  }
} // namespace

// macOS twin of testWin32NodeHandlerEnsureContract. Besides pinning context
// publication, reuse, relayout, and typed refusal through the native controller
// seam, the final leg discriminates PR #254's modal re-entrancy hazard: the
// handler must return the Node's context after afterAttach, not its stale local.
void testMacNodeHandlerEnsureContract()
{
  std::printf("\n==== [testMacNodeHandlerEnsureContract] start ====\n");
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  [NSApplication sharedApplication];
  verifyFixedColumnFitsAtBothScales();
  verifyTextColumnExtents();
  NSView *root = [[NSView alloc] initWithFrame:NSMakeRect(0, 0, 320, 240)];
  LOKA_VERIFY(root != nil);
  {
    MacScenePlatformController controller((void *)root, loka::app::RailMetrics());

    // -- Button: full contract through the root view's child census --
    loka::app::ButtonProps buttonProps;
    loka::app::ButtonNode button(buttonProps);

    loka::app::scene::LayoutState state;
    state.x = 10;
    state.y = 20;
    state.width = 100;
    state.height = 30;
    const NSUInteger childrenBeforeEnsure = countChildViews(root);
    LOKA_VERIFY(controller.prepareProjectedLayout(&button, state));

    loka::app::scene::NodeContext *ctx = button.getContext();
    LOKA_VERIFY(ctx != 0 && "ensure must publish the created context through setContext");
    LOKA_VERIFY(countChildViews(root) == childrenBeforeEnsure + 1);
    NSView *buttonView = [[root subviews] objectAtIndex:childrenBeforeEnsure];
    LOKA_VERIFY(frameEquals(buttonView, 10, 20, 100, 30));

    // Second ensure with new geometry: same context, same view population,
    // view moved by the relayout path -- not recreated.
    state.x = 40;
    state.y = 50;
    state.width = 120;
    state.height = 40;
    LOKA_VERIFY(controller.prepareProjectedLayout(&button, state));
    LOKA_VERIFY(button.getContext() == ctx && "re-ensure must reuse the existing context, not recreate it");
    LOKA_VERIFY(countChildViews(root) == childrenBeforeEnsure + 1 &&
                "re-ensure must not materialize another native view");
    LOKA_VERIFY(frameEquals(buttonView, 40, 50, 120, 40) &&
                "re-ensure must route through relayout so the view follows the requested geometry");

    // -- Text: same contract on a second native node kind --
    loka::app::TextProps textProps;
    loka::app::TextNode text(textProps);
    state.x = 5;
    state.y = 100;
    state.width = 200;
    state.height = 16;
    LOKA_VERIFY(controller.prepareProjectedLayout(&text, state));
    loka::app::scene::NodeContext *textCtx = text.getContext();
    LOKA_VERIFY(textCtx != 0 && "ensure must publish the created context through setContext");
    const NSUInteger childrenWithText = countChildViews(root);
    LOKA_VERIFY(childrenWithText == childrenBeforeEnsure + 2);
    LOKA_VERIFY(controller.prepareProjectedLayout(&text, state));
    LOKA_VERIFY(text.getContext() == textCtx);
    LOKA_VERIFY(countChildViews(root) == childrenWithText);

    // Alignment applies on the retained plain label without replacing it.
    NSTextField *textField = (NSTextField *)[[root subviews] objectAtIndex:childrenWithText - 1];
    const loka::app::TextAlign alignments[] = {
        loka::app::TEXT_ALIGN_CENTER, loka::app::TEXT_ALIGN_RIGHT, loka::app::TEXT_ALIGN_LEFT};
    const NSTextAlignment expected[] = {LOKA_MAC_TEXT_ALIGNMENT_CENTER, LOKA_MAC_TEXT_ALIGNMENT_RIGHT, LOKA_MAC_TEXT_ALIGNMENT_LEFT};
    for (int a = 0; a < 3; ++a)
    {
      text.props.blockStyle_.align(alignments[a]);
      textCtx->onPropsApplied();
      LOKA_VERIFY([textField alignment] == expected[a]);
      LOKA_VERIFY(text.getContext() == textCtx);
    }
    // Retained aligned -> undeclared restores AppKit's default alignment.
    text.props.blockStyle_ = loka::app::BlockStyle();
    textCtx->onPropsApplied();
    LOKA_VERIFY([textField alignment] == LOKA_MAC_TEXT_ALIGNMENT_NATURAL);
    LOKA_VERIFY(text.getContext() == textCtx);

    // -- TextEditor: installs its multiline context even with unavailable props --
    loka::app::TextEditorNode editor((loka::app::TextEditorProps()));
    LOKA_VERIFY(controller.prepareProjectedLayout(&editor, state));
    LOKA_VERIFY(editor.getContext());
    // -- ScrollBar: known unsupported kinds take the typed-refusal path --
    loka::app::ScrollBarProps scrollProps;
    loka::app::ScrollBarNode scrollBar(scrollProps);
    state.x = 5;
    state.y = 130;
    state.width = 120;
    state.height = 16;
    LOKA_VERIFY(!controller.prepareProjectedLayout(&scrollBar, state) &&
                "an unsupported kind must refuse, not project");
    LOKA_VERIFY(!scrollBar.getContext());
    LOKA_VERIFY(countChildViews(root) == childrenWithText + 1 &&
                "a refusal must not materialize a native view");

    // -- Re-entrancy: afterAttach replaces the just-published context --
    gReentrantCreates = 0;
    gReentrantAttachReads = 0;
    gReentrantAfterAttaches = 0;
    gReentrantDestroys = 0;
    MacReentrantEnsureHandler reentrantHandler;
    LOKA_VERIFY(controller.registerNodeHandler(&reentrantHandler));
    loka::app::ButtonNode reentrantButton(buttonProps);
    LOKA_VERIFY(!controller.prepareProjectedLayout(&reentrantButton, state) &&
                "ensure must re-read the node after re-entrant attach work replaces its context");
    LOKA_VERIFY(!reentrantButton.getContext());
    LOKA_VERIFY(gReentrantCreates == 1);
    LOKA_VERIFY(gReentrantAttachReads == 1);
    LOKA_VERIFY(gReentrantAfterAttaches == 1);
    LOKA_VERIFY(gReentrantDestroys == 1);

    std::printf("  button and text contexts reused; scrollbar refused; re-entrant ensure returned null\n");
    // Nodes leave scope before the controller. Their terminal facts remove
    // native views synchronously; the controller then drains retained objects.
  }
  LOKA_VERIFY(countChildViews(root) == 0);
  [root release];
  [pool drain];
  std::printf("==== [testMacNodeHandlerEnsureContract] PASSED ====\n");
}

#include "app/nodes/AttributedText.hpp"
#include "platform/MacNativeGeometry.hpp"
#include "support/LifecycleFactTestAccess.hpp"
#include "support/LokaAllocFailure.hpp"
#include "platform/String.hpp"

namespace
{
  NSTextField *AttributedField(NSView *root)
  {
    LOKA_VERIFY([[root subviews] count] == 1);
    return (NSTextField *)[[root subviews] objectAtIndex:0];
  }

  void VerifyAttributedHeight(loka::app::AttributedTextNode &node, MacScenePlatformController &controller,
                              NSView *root, short width)
  {
    loka::app::scene::LayoutState state;
    state.x = 3;
    state.y = 5;
    state.width = width;
    node.layoutProjected(&controller, state);
    NSTextField *field = AttributedField(root);
    const loka::macos::MacProjection &projection = controller.projection();
    const NSSize rendered = [[field cell] cellSizeForBounds:loka::macos::MacMeasurementBounds(
        projection.projectLength(state.x, state.x + width))];
    LOKA_VERIFY(state.height == projection.measurementToLu(rendered.height));
    LOKA_VERIFY(NSEqualRects([field frame], projection.projectFrame(
        loka::core::Frame(3, 5, width, state.height)).r));
  }

  class MacRefusedAttributedString : public loka::platform::String
  {
  public:
    virtual bool appendUtf8(std::string &) const { return false; }
  };
}

void testMacAttributedTextWholeLineProjection()
{
  using namespace loka::app;
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  [NSApplication sharedApplication];
  const RailMetrics metrics[] = {RailMetrics(), RailMetrics(Ratio(13, 12), Ratio(5, 4))};
  for (int scale = 0; scale < 2; ++scale)
  {
    NSView *root = [[NSView alloc] initWithFrame:NSMakeRect(0, 0, 320, 240)];
    {
      MacScenePlatformController controller((void *)root, metrics[scale]);
      LOKA_VERIFY(controller.textShaping() == WHOLE_LINE);
      const TextStyle small = FontSize<12>();
      const TextStyle large = FontSize<24>() + Italic;
      // A supplementary character makes UTF-8 byte offsets and UTF-16 ranges
      // disagree. Splitting the small run must not create a new styled range.
      const AttributedString value = Styled("A", small) + Styled("\xF0\x9F\x98\x80 ", small)
          + Styled("large italic words wrap here", large);
      AttributedTextProps props(value);
      props.blockStyle_ = BlockStyle().wrap(TEXT_WRAP_WORD);
      AttributedTextNode node(props);
      loka::app::scene::LayoutState state;
      state.width = 90;
      // Pre-fix red: gRefusedMacAttributedText rejects this ensure.
      LOKA_VERIFY(controller.prepareProjectedLayout(&node, state));
      LOKA_VERIFY(node.getContext() != 0);
      VerifyAttributedHeight(node, controller, root, 90);
      NSTextField *field = AttributedField(root);
      NSAttributedString *native = [field attributedStringValue];
      NSRange range;
      NSFont *first = [native attribute:NSFontAttributeName atIndex:0 effectiveRange:&range];
      LOKA_VERIFY([first isEqual:(NSFont *)controller.textFont(small)]);
      LOKA_VERIFY(range.location == 0 && range.length == 4);
      NSFont *second = [native attribute:NSFontAttributeName atIndex:4 effectiveRange:&range];
      LOKA_VERIFY([second isEqual:(NSFont *)controller.textFont(large)]);
      LOKA_VERIFY(range.location == 4 && range.length == [native length] - 4);
      const CGFloat narrowHeight = [field frame].size.height;
      VerifyAttributedHeight(node, controller, root, 260);
      LOKA_VERIFY([field frame].size.height < narrowHeight);
      loka::app::scene::NodeContext *context = node.getContext();
      node.props.blockStyle_ = BlockStyle().wrap(TEXT_WRAP_CHAR);
      context->onPropsApplied();
      VerifyAttributedHeight(node, controller, root, 90);
      LOKA_VERIFY([[field cell] lineBreakMode] == NSLineBreakByCharWrapping);
      NSParagraphStyle *paragraph = [[field attributedStringValue]
          attribute:NSParagraphStyleAttributeName atIndex:0 effectiveRange:0];
      LOKA_VERIFY([paragraph lineBreakMode] == NSLineBreakByCharWrapping);
      node.props.blockStyle_ = BlockStyle().wrap(TEXT_WRAP_NONE).truncation(TEXT_TRUNCATION_ELLIPSIS);
      context->onPropsApplied();
      VerifyAttributedHeight(node, controller, root, 90);
      LOKA_VERIFY([[field cell] lineBreakMode] == NSLineBreakByTruncatingTail);
      LOKA_VERIFY(![[field cell] wraps]);
      const TextAlign alignments[] = {TEXT_ALIGN_LEFT, TEXT_ALIGN_CENTER, TEXT_ALIGN_RIGHT};
      const NSTextAlignment expected[] = {LOKA_MAC_TEXT_ALIGNMENT_LEFT, LOKA_MAC_TEXT_ALIGNMENT_CENTER, LOKA_MAC_TEXT_ALIGNMENT_RIGHT};
      for (int a = 0; a < 3; ++a)
      {
        node.props.blockStyle_ = BlockStyle().wrap(TEXT_WRAP_NONE).align(alignments[a]);
        node.props.text(Styled("short", small));
        context->onPropsApplied();
        VerifyAttributedHeight(node, controller, root, 90);
        const CGFloat oneLine = [field frame].size.height;
        node.props.text(Styled("short\nline", small));
        context->onPropsApplied();
        VerifyAttributedHeight(node, controller, root, 90);
        LOKA_VERIFY([field frame].size.height > oneLine);
        LOKA_VERIFY(![[field cell] wraps] && ![[field cell] isScrollable]);
        if ([[field cell] respondsToSelector:@selector(usesSingleLineMode)])
          LOKA_VERIFY(![[field cell] usesSingleLineMode]);
        NSParagraphStyle *aligned = [[field attributedStringValue]
            attribute:NSParagraphStyleAttributeName atIndex:0 effectiveRange:0];
        LOKA_VERIFY([aligned alignment] == expected[a]);
      }
      LOKA_VERIFY(node.getContext() == context);
    }
    LOKA_VERIFY([[root subviews] count] == 0);
    [root release];
  }
  [pool drain];
}

void testMacAttributedTextRetainedLifecycle()
{
  using namespace loka::app;
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  [NSApplication sharedApplication];
  NSView *root = [[NSView alloc] initWithFrame:NSMakeRect(0, 0, 320, 240)];
  {
    MacScenePlatformController controller((void *)root, RailMetrics());
    AttributedTextProps props(Styled("before", FontSize<12>()));
    props.blockStyle_ = BlockStyle().wrap(TEXT_WRAP_WORD);
    AttributedTextNode node(props);
    // A node placed directly (not through the definition factory) carries no
    // props type id; applyPropsToNode's compatibility check needs it.
    node.setPropsTypeId(AttributedTextProps::staticTypeId());
    VerifyAttributedHeight(node, controller, root, 100);
    loka::app::scene::NodeContext *context = node.getContext();
    NSTextField *field = AttributedField(root);
    NSAttributedString *before = [[field attributedStringValue] retain];
    loka::app::scene::NotifySubtreeNodeDetached(&node);
    loka::app::scene::LifecycleFactTestAccess::DeliverFacts(&node);
    LOKA_VERIFY([field isHidden]);
    LOKA_VERIFY([[field attributedStringValue] isEqualToAttributedString:before]);
    loka::app::scene::NotifySubtreeNodeAttached(&node);
    loka::app::scene::LifecycleFactTestAccess::DeliverFacts(&node);
    LOKA_VERIFY(![field isHidden]);
    LOKA_VERIFY(node.getContext() == context);
    LOKA_VERIFY([[field attributedStringValue] isEqualToAttributedString:before]);
    [before release];
    loka::app::scene::NotifySubtreeNodeDetached(&node);
    loka::app::scene::LifecycleFactTestAccess::DeliverFacts(&node);
    AttributedTextProps replacement(Styled("updated while hidden", FontSize<24>() + Italic));
    replacement.blockStyle_ = BlockStyle().wrap(TEXT_WRAP_WORD);
    AttributedTextDefinition definition(replacement);
    LOKA_VERIFY(definition.applyPropsToNode(&node));
    VerifyAttributedHeight(node, controller, root, 100);
    LOKA_VERIFY([field isHidden]);
    loka::app::scene::NotifySubtreeNodeAttached(&node);
    loka::app::scene::LifecycleFactTestAccess::DeliverFacts(&node);
    LOKA_VERIFY(![field isHidden]);
    LOKA_VERIFY([[field stringValue] isEqualToString:@"updated while hidden"]);
    LOKA_VERIFY(node.getContext() == context);
    loka::app::scene::LifecycleFactTestAccess::MarkSubtreeRetired(&node);
    loka::app::scene::LifecycleFactTestAccess::DeliverFacts(&node);
    LOKA_VERIFY([[root subviews] count] == 0);
    LOKA_VERIFY([[field stringValue] length] == 0);
  }
  [root release];
  [pool drain];
}

void testMacAttributedTextRefusalClearsProjection()
{
  using namespace loka::app;
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  [NSApplication sharedApplication];
  NSView *root = [[NSView alloc] initWithFrame:NSMakeRect(0, 0, 320, 240)];
  {
    MacScenePlatformController controller((void *)root, RailMetrics());
    AttributedTextProps props(Styled("visible", FontSize<12>()));
    AttributedTextNode node(props);
    node.setPropsTypeId(AttributedTextProps::staticTypeId());
    VerifyAttributedHeight(node, controller, root, 100);
    NSTextField *field = AttributedField(root);
    const loka::core::String text("allocation refusal");
    loka::core::testing::failLokaAllocRaw("AttributedString", "Segments", 1);
    const AttributedString refused = Styled(text, Italic);
    loka::core::testing::allowLokaAllocRaw();
    LOKA_VERIFY(!refused.valid());
    LOKA_VERIFY(AttributedText(refused).applyPropsToNode(&node));
    loka::app::scene::LayoutState state;
    state.width = 100;
    node.layoutProjected(&controller, state);
    LOKA_VERIFY(state.height == 0 && [[field stringValue] length] == 0);
    LOKA_VERIFY(AttributedText(Styled("recovered", Italic)).applyPropsToNode(&node));
    VerifyAttributedHeight(node, controller, root, 100);
    const loka::core::String unreadable(loka::core::Managed<loka::platform::String>::Wrap(new MacRefusedAttributedString()));
    LOKA_VERIFY(AttributedText(Styled(unreadable, Italic)).applyPropsToNode(&node));
    node.layoutProjected(&controller, state);
    LOKA_VERIFY(state.height == 0 && [[field stringValue] length] == 0);
    LOKA_VERIFY(AttributedText(Styled("recovered again", Italic)).applyPropsToNode(&node));
    VerifyAttributedHeight(node, controller, root, 100);
  }
  [root release];
  [pool drain];
}

#include <objc/runtime.h>
#include "app/nodes/boundary/StdComposition.hpp"
#include "core/util/StateTrackerGuard.hpp"

// Both production measurement sites send this selector to NSTextFieldCell.
// Install on that class only (even when the SDK inherits it from NSCell).
static unsigned gMac970Measurements = 0;
@interface NSTextFieldCell (Loka970MeasurementCounter)
- (NSSize)loka970_cellSizeForBounds:(NSRect)bounds;
@end
@implementation NSTextFieldCell (Loka970MeasurementCounter)
- (NSSize)loka970_cellSizeForBounds:(NSRect)bounds
{
  ++gMac970Measurements;
  return [self loka970_cellSizeForBounds:bounds];
}
@end

namespace
{
  class MacMeasurementCounter
  {
  public:
    MacMeasurementCounter()
    {
      Class cell = [NSTextFieldCell class];
      Method inherited = class_getInstanceMethod(cell, @selector(cellSizeForBounds:));
      LOKA_VERIFY(inherited != 0);
      class_addMethod(cell, @selector(cellSizeForBounds:), method_getImplementation(inherited),
                      method_getTypeEncoding(inherited));
      this->original_ = class_getInstanceMethod(cell, @selector(cellSizeForBounds:));
      this->counting_ = class_getInstanceMethod(cell, @selector(loka970_cellSizeForBounds:));
      LOKA_VERIFY(this->original_ && this->counting_);
      method_exchangeImplementations(this->original_, this->counting_);
      gMac970Measurements = 0;
    }
    ~MacMeasurementCounter()
    {
      method_exchangeImplementations(this->original_, this->counting_);
    }
    unsigned calls() const { return gMac970Measurements; }
  private:
    Method original_;
    Method counting_;
    MacMeasurementCounter(const MacMeasurementCounter &);
    MacMeasurementCounter &operator=(const MacMeasurementCounter &);
  };

  template <class Leaf>
  void VerifyMacMeasurementReuse(Leaf &node, bool attributed)
  {
    using namespace loka::app;
    using namespace loka::app::scene;
    NSView *root = [[NSView alloc] initWithFrame:NSMakeRect(0, 0, 400, 400)];
    {
      MacScenePlatformController controller((void *)root, loka::macos::DefaultRailMetrics());
      MacMeasurementCounter counter;
      LayoutState state;
      state.width = 101;
      NotifySubtreeNodeAttached(&node);
      node.layoutProjected(&controller, state);
      NSTextField *field = AttributedField(root);
      LOKA_VERIFY(counter.calls() == 1); // Positive control: swizzle must observe the real call.
      const short height = state.height;
      LOKA_VERIFY(height > 0);
      NSAttributedString *before = [[field attributedStringValue] retain];
      state.x = 4; // Same endpoint phase at spaceScale 5/4.
      state.y = 23;
      state.height = 999;
      // Bypass ensure's plain refresh: only layout's placement can satisfy this pin.
      const short advance = node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == 1);
      LOKA_VERIFY(state.height == height);
      LOKA_VERIFY(advance == state.y + height + layout::FallbackControlMetrics::kVerticalSpacing);
      LOKA_VERIFY(NSEqualRects([field frame], controller.projection().projectFrame(
          loka::core::Frame(4, 23, 101, height)).r));
      if (attributed)
        LOKA_VERIFY([field attributedStringValue] == before);
      [before release];
      state.x = 1;
      LOKA_VERIFY(controller.projection().projectLength(0, 101).pt == 126);
      LOKA_VERIFY(controller.projection().projectLength(1, 102).pt == 127);
      unsigned calls = counter.calls();
      node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == calls + (attributed ? 1 : 0));
      state.width = 80;
      calls = counter.calls();
      node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == calls + 1);
      node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == calls + 1);
      NotifySubtreeNodeDetached(&node);
      LifecycleFactTestAccess::DeliverFacts(&node);
      LOKA_VERIFY([field isHidden]);
      before = [[field attributedStringValue] retain];
      NotifySubtreeNodeAttached(&node);
      LifecycleFactTestAccess::DeliverFacts(&node);
      LOKA_VERIFY(![field isHidden]);
      LOKA_VERIFY([[field attributedStringValue] isEqualToAttributedString:before]);
      [before release];
      calls = counter.calls();
      node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == calls + 1);
      node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == calls + 1);
      if (!attributed)
      {
        // Native presentation drift must be reconciled on a measurement hit.
        [field setAlignment:LOKA_MAC_TEXT_ALIGNMENT_RIGHT];
        [[field cell] setWraps:NO];
        [[field cell] setScrollable:YES];
        [[field cell] setLineBreakMode:NSLineBreakByClipping];
        [[field cell] setFont:[NSFont systemFontOfSize:30]];
        node.layout(&controller, state);
        LOKA_VERIFY(counter.calls() == calls + 1);
        LOKA_VERIFY([field alignment] == LOKA_MAC_TEXT_ALIGNMENT_LEFT);
        LOKA_VERIFY([[field cell] wraps] && ![[field cell] isScrollable]);
        LOKA_VERIFY([[field cell] lineBreakMode] == NSLineBreakByWordWrapping);
        LOKA_VERIFY([[[field cell] font] isEqual:(NSFont *)controller.textFont(FontSize<12>())]);
      }
      state.width = 0;
      node.layout(&controller, state);
      calls = counter.calls();
      node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == calls);
      // Node is caller-owned and must retire before this controller drains.
      LifecycleFactTestAccess::MarkSubtreeRetired(&node);
      LifecycleFactTestAccess::DeliverFacts(&node);
      LOKA_VERIFY([[root subviews] count] == 0);
      LOKA_VERIFY([[field stringValue] length] == 0);
      node.setContext(0);
    }
    [root release];
  }

  class MacMeasurementOwner : public loka::app::BoundaryNodeFor<MacMeasurementOwner>
  {
  public:
    MacMeasurementOwner() : loka::app::BoundaryNodeFor<MacMeasurementOwner>(
        loka::app::BoundaryPropsFor<MacMeasurementOwner>()) {}
    virtual void composeNode(loka::app::scene::NodeComposition &) {}
  };

  // A real CollectUtf8 refusal, with no production hook. Changing availability
  // models recovery of the same input; no props/State mark rescues the retry.
  class MacRetryString : public loka::platform::String
  {
  public:
    MacRetryString() : available_(true), attempts_(0) {}
    void available(bool value) { this->available_ = value; }
    unsigned attempts() const { return this->attempts_; }
    virtual bool appendUtf8(std::string &out) const
    {
      ++this->attempts_;
      if (!this->available_)
        return false;
      out.append("retry the same wrapped text");
      return true;
    }
  private:
    bool available_;
    mutable unsigned attempts_;
  };

  template <class Leaf>
  void VerifyMacMeasurementRefusal(Leaf &node, MacRetryString &source)
  {
    using namespace loka::app;
    using namespace loka::app::scene;
    NSView *root = [[NSView alloc] initWithFrame:NSMakeRect(0, 0, 320, 240)];
    {
      MacScenePlatformController controller((void *)root, RailMetrics());
      MacMeasurementCounter counter;
      LayoutState state;
      state.width = 100;
      node.layoutProjected(&controller, state);
      NSTextField *field = AttributedField(root);
      LOKA_VERIFY(counter.calls() == 1 && state.height > 0);
      source.available(false);
      state.width = 90; // Force a miss without mutating the logical input.
      node.layout(&controller, state);
      LOKA_VERIFY(state.height == 0 && [[field stringValue] length] == 0);
      LOKA_VERIFY(NSIsEmptyRect([field frame]));
      const unsigned refusedAttempts = source.attempts();
      node.layout(&controller, state);
      LOKA_VERIFY(source.attempts() > refusedAttempts);
      LOKA_VERIFY(counter.calls() == 1);
      source.available(true);
      node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == 2 && state.height > 0);
      LOKA_VERIFY([[field stringValue] isEqualToString:@"retry the same wrapped text"]);
      LOKA_VERIFY(!NSIsEmptyRect([field frame]));
      node.layout(&controller, state);
      LOKA_VERIFY(counter.calls() == 2);
      LifecycleFactTestAccess::MarkSubtreeRetired(&node);
      LifecycleFactTestAccess::DeliverFacts(&node);
      node.setContext(0);
    }
    [root release];
  }
}

void testMacPlainTextMeasurementReuse()
{
  using namespace loka::app;
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  [NSApplication sharedApplication];
  TextNode node((Text("words that wrap over several lines") + FontSize<12>()
                 + BlockStyle().wrap(TEXT_WRAP_WORD)).props);
  VerifyMacMeasurementReuse(node, false);
  [pool drain];
}

void testMacAttributedTextMeasurementReuse()
{
  using namespace loka::app;
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  [NSApplication sharedApplication];
  AttributedTextNode node((AttributedText(Styled("words that wrap over several lines", FontSize<12>()))
                           + BlockStyle().wrap(TEXT_WRAP_WORD)).props);
  VerifyMacMeasurementReuse(node, true);
  [pool drain];
}

void testMacTextMeasurementRefusalRecovery()
{
  using namespace loka::app;
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  [NSApplication sharedApplication];
  MacRetryString *source = new MacRetryString();
  const loka::core::String value(loka::core::Managed<loka::platform::String>::Wrap(source));
  TextNode plain((Text(value) + BlockStyle().wrap(TEXT_WRAP_WORD)).props);
  VerifyMacMeasurementRefusal(plain, *source);
  AttributedTextNode rich((AttributedText(Styled(value, FontSize<12>()))
                           + BlockStyle().wrap(TEXT_WRAP_WORD)).props);
  VerifyMacMeasurementRefusal(rich, *source);
  [pool drain];
}

void testMacTextMeasurementInputPublications()
{
  using namespace loka::app;
  using namespace loka::app::scene;
  using namespace loka::core;
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  [NSApplication sharedApplication];
  NSView *root = [[NSView alloc] initWithFrame:NSMakeRect(0, 0, 320, 240)];
  {
    MacScenePlatformController controller((void *)root, RailMetrics());
    MutableState<String> content(String::Literal("before wrapped words"));
    MutableState<TextStyle> style((FontSize<12>()));
    MutableState<AttributedString> rich(Styled("before wrapped words", FontSize<12>()));
    PushStateTracker tracker;
    tracker.addState(&content);
    tracker.addState(&style);
    tracker.addState(&rich);
    MacMeasurementOwner owner;
    TextNode plain(((Text(&content) + &style) + BlockStyle().wrap(TEXT_WRAP_WORD)).props);
    AttributedTextNode attributed((AttributedText(&rich) + BlockStyle().wrap(TEXT_WRAP_WORD)).props);
    plain.setPropsTypeId(TextProps::staticTypeId());
    attributed.setPropsTypeId(AttributedTextProps::staticTypeId());
    BoundaryNode::declareBoundaryDirtySources(&plain, &owner);
    BoundaryNode::declareBoundaryDirtySources(&attributed, &owner);
    MacMeasurementCounter counter;
    LayoutState state;
    state.width = 100;
    plain.layoutProjected(&controller, state);
    attributed.layoutProjected(&controller, state);
    LOKA_VERIFY(counter.calls() == 2);
    NSTextField *plainField = (NSTextField *)[[root subviews] objectAtIndex:0];
    NSTextField *richField = (NSTextField *)[[root subviews] objectAtIndex:1];
    {
      StateTrackerGuard guard(&tracker);
      content.set(String::Literal("after wrapped words"));
      rich.set(Styled("after wrapped words", FontSize<12>()));
    }
    plain.layout(&controller, state);
    attributed.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 4);
    LOKA_VERIFY([[plainField stringValue] isEqualToString:@"after wrapped words"]);
    LOKA_VERIFY([[richField stringValue] isEqualToString:@"after wrapped words"]);
    {
      StateTrackerGuard guard(&tracker);
      style.set(FontSize<24>());
      rich.set(Styled("after wrapped words", FontSize<24>()));
    }
    plain.layout(&controller, state);
    attributed.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 6);
    LOKA_VERIFY(((Text(&content) + &style) + BlockStyle().wrap(TEXT_WRAP_WORD).align(TEXT_ALIGN_RIGHT))
                    .applyPropsToNode(&plain));
    LOKA_VERIFY((AttributedText(&rich) + BlockStyle().wrap(TEXT_WRAP_WORD).align(TEXT_ALIGN_RIGHT))
                    .applyPropsToNode(&attributed));
    plain.layout(&controller, state);
    attributed.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 8);
    LOKA_VERIFY([plainField alignment] == LOKA_MAC_TEXT_ALIGNMENT_RIGHT);
    NSParagraphStyle *paragraph = [[richField attributedStringValue]
        attribute:NSParagraphStyleAttributeName atIndex:0 effectiveRange:0];
    LOKA_VERIFY([paragraph alignment] == LOKA_MAC_TEXT_ALIGNMENT_RIGHT);
    plain.layout(&controller, state);
    attributed.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 8);
    // Retained content/source/style replacement and style removal all mark.
    LOKA_VERIFY((Text("replacement words") + FontSize<12>() + BlockStyle().wrap(TEXT_WRAP_WORD))
                    .applyPropsToNode(&plain));
    LOKA_VERIFY((AttributedText(Styled("replacement words", FontSize<12>()))
                + BlockStyle().wrap(TEXT_WRAP_WORD)).applyPropsToNode(&attributed));
    plain.layout(&controller, state);
    attributed.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 10);
    LOKA_VERIFY([[plainField stringValue] isEqualToString:@"replacement words"]);
    LOKA_VERIFY([[richField stringValue] isEqualToString:@"replacement words"]);
    LOKA_VERIFY(Text("unstyled").applyPropsToNode(&plain));
    plain.layout(&controller, state);
    LOKA_VERIFY([plainField alignment] == LOKA_MAC_TEXT_ALIGNMENT_NATURAL);
    LOKA_VERIFY(state.height == layout::FallbackControlMetrics::kTextHeight);
    LOKA_VERIFY((Text("activated words") + FontSize<24>() + BlockStyle().wrap(TEXT_WRAP_WORD))
                    .applyPropsToNode(&plain));
    plain.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 11);
    // Parked publication plus reattach: table remains, but return consumes the mark.
    NotifySubtreeNodeDetached(&attributed);
    LifecycleFactTestAccess::DeliverFacts(&attributed);
    LOKA_VERIFY([richField isHidden]);
    LOKA_VERIFY((AttributedText(&rich) + BlockStyle().wrap(TEXT_WRAP_WORD)).applyPropsToNode(&attributed));
    {
      StateTrackerGuard guard(&tracker);
      rich.set(Styled("changed while parked", FontSize<12>()));
    }
    NotifySubtreeNodeAttached(&attributed);
    LifecycleFactTestAccess::DeliverFacts(&attributed);
    attributed.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 12);
    LOKA_VERIFY([[richField stringValue] isEqualToString:@"changed while parked"]);
    attributed.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 12);
    LOKA_VERIFY(((Text(&content) + &style) + BlockStyle().wrap(TEXT_WRAP_WORD)).applyPropsToNode(&plain));
    plain.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 13);
    NotifySubtreeNodeDetached(&plain);
    LifecycleFactTestAccess::DeliverFacts(&plain);
    LOKA_VERIFY([plainField isHidden]);
    {
      StateTrackerGuard guard(&tracker);
      content.set(String::Literal("plain changed while parked"));
    }
    NotifySubtreeNodeAttached(&plain);
    LifecycleFactTestAccess::DeliverFacts(&plain);
    plain.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 14);
    LOKA_VERIFY([[plainField stringValue] isEqualToString:@"plain changed while parked"]);
    plain.layout(&controller, state);
    LOKA_VERIFY(counter.calls() == 14);
    owner.clearObservedStateEntries();
  }
  LOKA_VERIFY([[root subviews] count] == 0);
  [root release];
  [pool drain];
}
