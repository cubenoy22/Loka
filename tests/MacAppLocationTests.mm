#include "MacAppLocationTests.hpp"
#include "support/TestVerify.hpp"

#include <Foundation/Foundation.h>
#include <objc/runtime.h>

#include <cassert>
#include <cstdio>
#include <cstring>

#include "core/io/File.hpp"
#include "platform/file/AppLocation.hpp"
#include "platform/file/FileIO.hpp"

@interface NSFileManager (LokaSidecarTests)
- (BOOL)loka_testRefuseWritableFileAtPath:(NSString *)path;
@end

@implementation NSFileManager (LokaSidecarTests)
- (BOOL)loka_testRefuseWritableFileAtPath:(NSString *)path
{
  (void)path;
  return NO;
}
@end

namespace
{
  class ScopedMethodExchange
  {
  public:
    ScopedMethodExchange(Class owner, SEL first, SEL second)
        : first_(class_getInstanceMethod(owner, first)), second_(class_getInstanceMethod(owner, second)), active_(false)
    {
      if (this->first_ && this->second_)
      {
        method_exchangeImplementations(this->first_, this->second_);
        this->active_ = true;
      }
    }

    ~ScopedMethodExchange()
    {
      if (this->active_)
      {
        method_exchangeImplementations(this->first_, this->second_);
      }
    }

    bool active() const { return this->active_; }

  private:
    ScopedMethodExchange(const ScopedMethodExchange &);
    ScopedMethodExchange &operator=(const ScopedMethodExchange &);

    Method first_;
    Method second_;
    bool active_;
  };
} // namespace

void testMacApplicationItemNamesResourceDirectory()
{
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  NSString *executablePath = [[NSBundle mainBundle] executablePath];
  NSString *executableDirectory = [executablePath stringByDeletingLastPathComponent];
  NSString *resourcePath = [[NSBundle mainBundle] resourcePath];
  assert(executablePath);
  assert(executableDirectory);
  assert([resourcePath isEqualToString:executableDirectory]);

  NSString *name = @"loka-application-location.bin";
  NSString *fixturePath = [executableDirectory stringByAppendingPathComponent:name];
  const unsigned char expected[] = {0x19, 0x9A, 0x02};
  NSData *payload = [NSData dataWithBytes:expected length:sizeof(expected)];
  LOKA_VERIFY([payload writeToFile:fixturePath atomically:NO]);

  const char *nameUtf8 = [name UTF8String];
  assert(nameUtf8);
  const loka::file::File item = loka::file::File::Application()
                                << loka::file::File(loka::core::String::Utf8(nameUtf8, std::strlen(nameUtf8)));
  loka::platform::file::FileHandle handle;
  LOKA_VERIFY(loka::platform::file::ResolveApplicationItem(item, handle));

  const char *pathUtf8 = [fixturePath UTF8String];
  assert(pathUtf8);
  assert(handle.displayPath.equals(loka::core::String::Utf8(pathUtf8, std::strlen(pathUtf8))));

  std::FILE *opened = loka::platform::file::OpenRead(handle.displayPath);
  assert(opened);
  unsigned char actual[sizeof(expected)] = {0};
  LOKA_VERIFY(std::fread(actual, 1, sizeof(actual), opened) == sizeof(actual));
  LOKA_VERIFY(std::fclose(opened) == 0);
  assert(std::memcmp(actual, expected, sizeof(actual)) == 0);

  LOKA_VERIFY([[NSFileManager defaultManager] removeItemAtPath:fixturePath error:nil]);
  [pool drain];
}

void testMacApplicationSidecarNamesBundleParent()
{
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  NSString *bundlePath = [[NSBundle mainBundle] bundlePath];
  assert(bundlePath);
  NSString *name = @"_loka-sidecar-write-test.tmp";
  NSString *expected = [[bundlePath stringByDeletingLastPathComponent] stringByAppendingPathComponent:name];
  [[NSFileManager defaultManager] removeItemAtPath:expected error:nil];

  loka::platform::file::FileHandle handle;
  LOKA_VERIFY(loka::platform::file::ResolveApplicationSidecar(
      loka::file::File::Application() << loka::file::File("_loka-sidecar-write-test.tmp"), handle));
  const char *expectedUtf8 = [expected UTF8String];
  assert(expectedUtf8);
  assert(handle.displayPath.equals(loka::core::String::Utf8(expectedUtf8, std::strlen(expectedUtf8))));

  const unsigned char payload[] = {0x4C, 0x4F, 0x4B, 0x41};
  std::FILE *opened = loka::platform::file::OpenWriteTruncate(handle);
  assert(opened);
  LOKA_VERIFY(std::fwrite(payload, 1, sizeof(payload), opened) == sizeof(payload));
  LOKA_VERIFY(loka::platform::file::FlushWrite(opened, handle));
  LOKA_VERIFY(std::fclose(opened) == 0);
  NSData *actual = [NSData dataWithContentsOfFile:expected];
  assert(actual);
  assert([actual length] == sizeof(payload));
  assert(std::memcmp([actual bytes], payload, sizeof(payload)) == 0);
  LOKA_VERIFY([[NSFileManager defaultManager] removeItemAtPath:expected error:nil]);
  [pool drain];
}

void testMacApplicationSidecarDeclinesWhenParentReportsReadOnly()
{
  NSAutoreleasePool *pool = [[NSAutoreleasePool alloc] init];
  NSFileManager *manager = [NSFileManager defaultManager];
  ScopedMethodExchange exchange([manager class],
                                @selector(isWritableFileAtPath:),
                                @selector(loka_testRefuseWritableFileAtPath:));
  LOKA_VERIFY(exchange.active());

  loka::platform::file::FileHandle handle;
  LOKA_VERIFY(!loka::platform::file::ResolveApplicationSidecar(
      loka::file::File::Application() << loka::file::File("LOG.TXT"), handle));
  assert(handle.displayPath.empty());
  [pool drain];
}
